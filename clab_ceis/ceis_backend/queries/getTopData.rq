PREFIX : <http://www.semanticweb.org/sophi/ontologies/2024/10/untitled-ontology-20/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT
  (STRAFTER(STR(?recipe), "http://www.semanticweb.org/sophi/ontologies/2024/10/untitled-ontology-20/") AS ?recipeName)
  (STRAFTER(STR(?fabricBlockDesign), "http://www.semanticweb.org/sophi/ontologies/2024/10/untitled-ontology-20/") AS ?fabricBlockDesignName)
  ?requiredAmount ?availableAmount
  (IF(?availableAmount >= ?requiredAmount, "Yes", "No") AS ?readyForAssembly)
WHERE {
  # Fetch top designs
  ?design rdf:type/rdfs:subClassOf* :TopDesign .
  
  # Get recipe from design
  ?design :hasRecipe ?recipe .
  
  # Link recipe to its FabricBlockRequirement
  ?recipe :hasMaterialRequirement ?requirement .
  
  # Ensure requirement is specifically a FabricBlockRequirement
  ?requirement rdf:type :FabricBlockRequirement .
  
  # Link requirement to a specific FabricBlockDesign and its required amount
  ?requirement :requiresFabricBlockDesign ?fabricBlockDesign .
  ?requirement :fabricBlockAmount ?requiredAmount .
  
  # Count the available FabricBlocks for the specified FabricBlockDesign
  {
    SELECT ?fabricBlockDesign (COUNT(?fabricBlock) AS ?availableAmount)
    WHERE {
      ?fabricBlock rdf:type :FabricBlock .
      ?fabricBlock :followsFabricBlockDesign ?fabricBlockDesign .
    }
    GROUP BY ?fabricBlockDesign
  }
}